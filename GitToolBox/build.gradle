import java.nio.file.Paths
import java.text.SimpleDateFormat

buildscript {
    repositories {
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        mavenCentral()
    }

    dependencies {
        //use eap because it fixes internal access between main and test
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.4.12'
    id 'com.palantir.git-version' version '0.12.2'
    id 'ch.netzwerg.release' version '1.2.5'
    id 'org.jlleitschuh.gradle.ktlint' version '9.0.0'
}

repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    mavenCentral()
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'idea'
apply plugin: "java"
apply plugin: "kotlin"

def javaVersion = JavaVersion.VERSION_1_8
def kotlinJvmTarget = "1.8"
def encoding = 'UTF-8'

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

def ci = findProperty('CI') != null
def publish = Boolean.valueOf(String.valueOf(findProperty('publish')))

configurations {
    testAgent {
        transitive = false
    }
}

def ideaCacheDir = Paths.get(System.getProperty('user.home')).resolve('intellij-gradle-cache').toAbsolutePath().toString()
println("Idea cache dir: " + ideaCacheDir)
println("Build type: " + buildType())

intellij {
    version = ideaVersion
    type = ideaType
    updateSinceUntilBuild = false
    downloadSources = true
    plugins = ['java', 'git4idea', 'gr.jchrist.gitextender:0.5.2']
    ideaDependencyCachePath = ideaCacheDir
    instrumentCode = true
}

compileJava.options.encoding = encoding
compileTestJava.options.encoding = encoding

compileKotlin {
    kotlinOptions {
        jvmTarget = kotlinJvmTarget
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = kotlinJvmTarget
    }
}

patchPluginXml {
    changeNotes "${file('change-notes.html').getText(encoding)}"
    pluginDescription "${file('description.html').getText(encoding)}"
            .replace("RELEASE_DATETIME", buildTime())
            .replace("RELEASE_TYPE", buildType())
}

if (publish) {
    apply from: 'publish.gradle'
    publishPlugin {
        token = project.publishToken
        username = project.publishUsername
        password = project.publishPassword
        channels = publishChannels()
    }    
}

jar {
    manifest {
        attributes(
            'Name': project.name,
            'Specification-Title': project.name,
            'Specification-Vendor': 'Lukasz Zielinski (zieluuuu@gmail.com)',
            'Specification-Version': project.version,
            'Implementation-Title': project.name,
            'Implementation-Vendor': 'Lukasz Zielinski (zieluuuu@gmail.com)',
            'Implementation-Version': versionDetails().gitHash,
            'Build-Date': buildTime()
        )
    }
}

release {
    tagPrefix = ''
    dependsOn buildPlugin
}

apply plugin: 'jacoco'
jacoco {
    toolVersion = "${jacocoVersion}"
}

// workaround for missing coverage report with JUnit 5 (https://stackoverflow.com/a/44907857)
// as 'test' task cleans generated report
jacocoTestReport {
    executionData.setFrom(fileTree(dir: "${project.buildDir.name}/jacoco-junit5", include: ['*.exec']))
}

test {
    jacoco {
        destinationFile = file("${project.buildDir.name}/jacoco-junit5/test.exec")
    }

    useJUnitPlatform {
        //prevent running extensions as Junit 3 tests because of naming convention with TestCase
        exclude('zielu/junit5/**')
        includeTags = ['fast']
        reports {
            html.enabled = false
        }
    }
}

task integrationTest(type: Test) {
    jacoco {
        destinationFile = file("${project.buildDir.name}/jacoco-junit5/test-integration.exec")
    }

    useJUnitPlatform {
        //prevent running extensions as Junit 3 tests because of naming convention with TestCase
        exclude('zielu/junit5/**')
        includeTags = ['integration']
        reports {
            html.enabled = false
        }
    }
}

apply plugin: 'checkstyle'
checkstyle {
    toolVersion = "${checkstyleVersion}"
    maxErrors = 0
    maxWarnings = 0
    /*configFile = project(':').file('config/checkstyle/lax_checkstyle.xml')*/
    configProperties = [ "suppressionFile" : project(':').file('config/checkstyle/suppressions.xml')]
}

ktlint {
    additionalEditorconfigFile = project(':').file("config/ktlint/.editorconfig")
    coloredOutput = false
    reporters {
        reporter "checkstyle"
    }
}

task buildAll {
    dependsOn check, integrationTest, buildPlugin
    tasks.findByName('integrationTest').mustRunAfter 'check'
}

task buildFast {
    dependsOn check, buildPlugin
}

task codeCoverage {
    dependsOn check, integrationTest, jacocoTestReport
    tasks.findByName('integrationTest').mustRunAfter 'check'
}

dependencies {
    checkstyle ("com.puppycrawl.tools:checkstyle:${checkstyleVersion}")
    testAgent ("org.jacoco:org.jacoco.agent:${jacocoVersion}:runtime")

    implementation ("org.jodd:jodd-core:${joddVersion}")
    implementation ("com.google.guava:guava:${guavaVersion}")
    implementation ("org.apache.commons:commons-lang3:${commonsLang3Version}")
    implementation ("io.dropwizard.metrics:metrics-core:${metricsVersion}") {
        exclude group: 'org.slf4j', module:'slf4j-api'
    }
    implementation ("io.dropwizard.metrics:metrics-jmx:${metricsVersion}") {
        exclude group: 'org.slf4j', module:'slf4j-api'
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testImplementation ("org.eclipse.jgit:org.eclipse.jgit:${jgitVersion}")

    testImplementation ("org.mockito:mockito-core:${mockitoVersion}")
    testImplementation ("org.mockito:mockito-junit-jupiter:${mockitoVersion}")

    testImplementation ("io.mockk:mockk:${mockkVersion}")

    testImplementation ("org.assertj:assertj-core:${assertjCoreVersion}")
    testImplementation ("org.slf4j:slf4j-api:${slf4jVersion}")

    testImplementation ("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testImplementation ("org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")

    testRuntimeOnly ("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    //testRuntimeOnly ("org.slf4j:slf4j-simple:${slf4jVersion}")
}


def static buildTime() {
    def df = new SimpleDateFormat("yyyyMMdd.HHmmss")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

def isStableBuild() {
    def details = versionDetails()
    return details.branchName ==~ /(master|\d+-release)/
}

def buildType() {
    return isStableBuild() ? "Stable" : "EAP"
}

def publishChannels() {
    return isStableBuild() ? ['default', 'eap'] : ['eap']
}
